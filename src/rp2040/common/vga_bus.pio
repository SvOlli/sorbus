; Copyright (c) 2025 Sven Oliver "SvOlli" Moll
;
; SPDX-License-Identifier: GPL-3.0-or-later
;

; this code needs to run on a RP2040
.pio_version 0

.define public CS        5
.define public CLK       27
.define public INPINS    11
.define public OUTPINS   8

.program vga_bus_wait

.wrap_target
loop:
   wait 0 gpio CS       ; first wait for CS to go low
   wait 1 gpio CLK      ; wait for CLK to go high
   in pins, INPINS      ; read databus + 2 dummy bits + A0 into isr
   mov osr, !null
   out pins, OUTPINS    ; dummy output on databus: $FF, everything pulled up
                        ; intended to be overwritten by C code during write,
                        ; ignored during read
   out pindirs, OUTPINS ; set datapins to output, just in case
   irq nowait 0         ; now call C code IRQ handler
   wait 0 gpio CLK      ; wait for CLK
   mov osr, null        ; cleanup by
   out pindirs, OUTPINS ; setting databus to input again
.wrap
