; Copyright (c) 2025 Sven Oliver "SvOlli" Moll
;
; SPDX-License-Identifier: GPL-3.0-or-later
;
; discussion:
; https://forums.raspberrypi.com/viewtopic.php?p=2304792#p2304792

; this code needs to run on an RP2040
.pio_version 0

.define public CS1       10
.define public CS0       11
.define public CLK       8
.define public R_W       9
.define public DATABUS0  0
.define public ADDRBUS0  12
.define public OUTPINS   8
.define public INPINS    20

.program sound_bus_wait

; step 1: wait for system bus
.wrap_target
loop:
   wait 1 gpio CS1      ; check for CS1 to go high
   jmp pin loop         ; check for CS2 to go low
   wait 1 gpio CLK [2]  ; wait for CLK
   mov isr, pins        ; fetch data from bus
   mov osr, !null
   out pins, OUTPINS    ; dummy output on databus: $FF
   out pindirs, OUTPINS ; set datapins to output, just in case
   push                 ; now let C code handle it
   wait 0 gpio CLK      ; wait for CLK
   mov osr, null        ; cleanup by
   out pindirs, OUTPINS ; setting databus to input again
.wrap

